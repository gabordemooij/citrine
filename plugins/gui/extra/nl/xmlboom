# Titel.......: Xmlboom
# Auteur......: Gabor de Mooij (c) copyright 2025
# Datum.......: 28 januari 2025
# Licentie....: BSD-3/MIT
# Versie......: 1.0
#
# Beschrijving:
# Xmlboom is een hulpprogramma om XML-bomen
# te bouwen die verweven zijn met reguliere programmacode.
# 
# Benodigdheden:
# - supplementnl

>> XML-boom := Object nieuw.

XML-boom bij: ['nieuw'] doen: {
	<- zelf nieuw voorbereiden.
}.

XML-boom bij: ['voorbereiden'] doen: {
	zelf opschonen.
	eigen attribuutnamen   := Lijst nieuw.
	eigen attribuutwaarden := Lijst nieuw.
	eigen elementnamen     := Lijst nieuw.
}.

XML-boom bij: ['opschonen'] doen: {
	eigen tekstmodus   := Nee.
	eigen uitkomst     := [''].
	eigen elementen    := Reeks nieuw.
	eigen elementmodus := Nee.
}.

XML-boom bij: ['elementen:attributen:waarden:'] doen: { :elementen :attributen :waarden
	eigen attribuutnamen   := attributen.
	eigen attribuutwaarden := waarden.
	eigen elementnamen     := elementen.
}.


XML-boom bij: ['bericht:'] doen: { :bericht
	
	>> elementnaam := (
		eigen elementnamen ? bericht of-anders: bericht
	).

	eigen elementen toevoegen: elementnaam.
	eigen uitkomst toevoegen: (
		['<element '] element: elementnaam 
	).
	eigen elementmodus := Nee.
}.

XML-boom bij: ['bericht:argument:'] doen: { :bericht :argument
	bericht := bericht - [':'].
	eigen tekstmodus ja: {
		eigen tekstmodus := Nee.
		eigen uitkomst toevoegen: argument.
	}, anders: {
		eigen uitkomst toevoegen: (
			['%attribuut%="%waarde%" ']
				%attribuut%: (
					eigen attribuutnamen ? bericht of-anders: bericht
				),
				%waarde%: (
					eigen attribuutwaarden ? argument of-anders: argument
				)
		).
	}.
}.

XML-boom bij: ['xml:'] doen: { code.
	eigen uitkomst toevoegen: code.
}.


XML-boom bij: ['elementen'] doen: {
	eigen uitkomst toevoegen: ['>\n'].
	eigen elementmodus := Ja.
}.

XML-boom bij: ['tekstueel'] doen: {
	eigen tekstmodus := Ja.
}.


XML-boom bij: ['sluit'] doen: {
	>> slot := eigen elementen rknip.
	eigen elementmodus ja: {
		eigen uitkomst toevoegen: (['\n</element>'] element: slot).
	}, anders: {
		eigen uitkomst toevoegen: (['></element>'] element: slot).
		eigen elementmodus := Ja.
	}.
}.





XML-boom bij: ['tekst'] doen: { <- eigen uitkomst. }.

