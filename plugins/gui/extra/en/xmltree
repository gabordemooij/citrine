# Title.......: XML-tree
# Author......: Gabor de Mooij (c) copyright 2025
# Date........: 28 januari 2025
# License.....: BSD-3/MIT
# Version.....: 1.0
#
# Description:
# This library provides an XML tree generator object that you
# can use to create XML by sending generic messages. This
# allows you to mix XML with native Citrine code as if it were
# one language. No need for XML syntax!
#
# Dependencies:
# - supplementen

>> XML-tree := Object new.

XML-tree on: ['new'] do: { 
	<- self new init.
}.

XML-tree on: ['init'] do: {
	own attrs       := Dict new.
	own values      := Dict new.
	own names       := Dict new.
	self clear.	
}.

XML-tree on: ['clear'] do: {
	own textmode    := False.
	own str         := [''].
	own tags        := List new.
	own elementmode := False.
}.

XML-tree on: ['names:attributes:values:'] do: { :names :attrs :values
	own attrs := attrs.
	own values := values.
	own names := names.
}.


XML-tree on: ['respond:'] do: { :message
	>> tag := (own names ? message otherwise: message).
	own tags append: tag.
	own str append: (
		['<tag '] tag: tag 
	).
	own elementmode := False.
}.

XML-tree on: ['respond:and:'] do: { :message :arg
	message := message - [':'].
	own textmode true: {
		own textmode := False.
		own str append: arg.
	}, else: {
		own str append: (
			['%attribute%="%value%" ']
				%attribute%: (own attrs ? message otherwise: message),
				%value%: (own values ? arg otherwise: arg)
		).
	}.
}.


XML-tree on: ['elements'] do: { :message :arg
	own str append: ['>\n'].
	own elementmode := True.
}.

XML-tree on: ['textual'] do: { :message :arg
	own textmode := True.
}.

XML-tree on: ['close'] do: { :message :arg
	>> end := own tags pop.
	own elementmode true: {
		own str append: (['\n</tag>'] tag: end).
	}, else: {
		own str append: (['></tag>'] tag: end).
		own elementmode := True.
	}.
}.

XML-tree on: ['text'] do: { <- own str. }.

XML-tree on: ['no-xml:'] do: { :str
	<- str text
	replace: ['&'] with: ['&amp;'],
	replace: ['<'] with: ['&lt;'],
	replace: ['>'] with: ['&gt;'],
	replace: ['"'] with: ['&quot;'],
	replace: ['''] with: ['&apos;'].
}.
