# Titel.......: GUINL
# Auteur......: Gabor de Mooij (c) copyright 2025
# Datum.......: 2025-03-14
# Licentie....: BSD-3/MIT
# Versie......: 1.1
#
# Beschrijving:
# GUINL is een voorbeeldprogramma van een GUI in Citrine/NL.
#
# Benodigdheden:
# - xmlboom
# - lvglnl
# - supplementnl

>> gui := Gui nieuw.

gui
	gebruik: 
		Bestandslocatie supplementnl,
	gebruik:
		Bestandslocatie xmlboom,
	gebruik: 
		Bestandslocatie lvglnl.

>> behang := 
	Plaatje nieuw: ['guidemo.png'].

>> grappig-letterype := 
	Lettertype nieuw
	bron: ['Shortcake.ttf'] grootte: 40.

>> wit := Kleur 
	nieuw 
	rood: 255
	groen: 255
	blauw: 255.
	
>> xmldemo := 
	XML-boom 
	geen-xml: ['<lv_label text="arbol XML"></lv_label>'].

gui bij: ['klik:'] doen: { :id
	(id = 2) ja: {
		>> xml := 
		
		['
		<component>
		<view extends="lv_obj" height="100%" width="100%" style_bg_opa="0">
		<lv_obj height="100%" width="100%">
		'] + (gui invulwaarde: 1) + ['
		</lv_obj>
		</view>
		</component>
		'].
		
		zelf xml: xml naam: ['x'] bij: ['3'].
	}.

}.

>> boomstructuur := LVGLNL nieuw.
boomstructuur
	component
	elementen
		weergave
		baseer-op: ['object'],
		id: 100,
		interactief: Ja,
		hoogte: ['100%'],
		breedte: ['100%'],
		achtergrond: behang,
		elementen
			object
			achtergrondsdekking: 0,
			interactief: Ja,
			rand: 0,
			breedte: ['100%'],
			hoogte: ['100%'],
			indeling: ['flex'],
			flexstroom: ['kolom'],
			elementen
				label
				lettertype: grappig-letterype,
				kleur: wit,
				tekst: ['De XML-BOOM'],
				sluit
				tekstveld
				breedte: ['100%'],
				id: ['1'],
				interactief: Ja,
				selecteerbaar: Ja,
				tekst: xmldemo,
				sluit
				knop
				interactief: Ja,
				id: ['2'],
				elementen
					label
					tekst: ['tekenen'],
					sluit
				sluit
				object
				id: ['3'],
				achtergrondsdekking: 0,
				rand: 0,
				breedte: ['100%'],
				hoogte: ['50%'],
				sluit
			sluit
		sluit
	sluit.

gui 
breedte: 400 hoogte: 800,
scherm: boomstructuur tekst.
