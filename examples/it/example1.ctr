✎ scrivi: ‘Generatore di password’, fermare.
☞ contare ≔ 0.
{
	✎ scrivi: ‘Quanti caratteri (16-30)?’, fermare.
	contare ≔ Programma chiedi numero assoluto.

	(contare < 16) vero: {
		✎ scrivi: ‘Non è sufficiente, inserisci un numero compreso tra 16 e 30.’, fermare.
	}.
	(contare > 30) vero: {
		✎ scrivi: ‘È troppo lungo, alcuni sistemi potrebbero rifiutare le password così a lungo.’, fermare.
	}.
} mentre: { ↲ (contare < 16 _o: contare > 30). }.
☞ personaggi ≔ ‘acdefghjkmnprtwx2345789@#&’.
☞ parola d'ordine ≔ ‘’.
{
	☞ char ≔ 
		personaggi carattere: ( 
		Numero
		tra e: 1
		__: personaggi lunghezza
	).
	parola d'ordine aggiungere: char.
} × contare.
✎ scrivi: parola d'ordine, fermare.