# Título.......: Árvore xml
# Autor........: Gabor de Mooij (c) copyright 2025
# Data.........: 28 de janeiro de 2025
# Licença......: BSD-3/MIT
# Versão.......: 1.0
#
# Descrição:
# Xmlboom é um utilitário para criar árvores XML
# para construir que estão interligados com código de programa regular.
#
# Abastecimentos:
# - suplementopt

>> Árvore-xml := Objeto novo.

Árvore-xml em: ['novo'] fazer: { <- eu novo preparar. }.

Árvore-xml em: ['preparar'] fazer: {
	eu limpar.
	próprio nomes-de-atributos := Mapa novo.
	próprio valores-de-atributos := Mapa novo.
	próprio nomes-dos-elementos := Mapa novo.
}.

Árvore-xml em: ['limpar'] fazer: {
	próprio modo-de-texto := Falso.
	próprio resultado := [''].
	próprio elementos := Listar novo.
	próprio modo-de-elemento := Falso.
}.

Árvore-xml em: ['elementos:atributos:valores:'] fazer: { :elementos :atributos :valores
	próprio nomes-de-atributos := atributos.
	próprio valores-de-atributos := valores.
	próprio nomes-dos-elementos := elementos.
}.


Árvore-xml em: ['responder:'] fazer: { :mensagem
	
	>> nome-do-elemento := (
		próprio nomes-dos-elementos ? mensagem ou-então: mensagem
	).

	próprio elementos acrescentar: nome-do-elemento.
	próprio resultado acrescentar: (
		['<elemento '] elemento: nome-do-elemento 
	).
	próprio modo-de-elemento := Falso.
}.

Árvore-xml em: ['responder:e:'] fazer: { :mensagem :argumento
	mensagem := mensagem - [':'].
	próprio modo-de-texto verdadeiro: {
		próprio modo-de-texto := Falso.
		próprio resultado acrescentar: argumento.
	}, senão: {
		próprio resultado acrescentar: (
			['%atributo%="%valor%" ']
				%atributo%: (
					próprio nomes-de-atributos ? mensagem ou-então: mensagem
				),
				%valor%: (
					próprio valores-de-atributos ? argumento ou-então: argumento
				)
		).
	}.
}.

Árvore-xml em: ['xml:'] fazer: { :código
	próprio resultado acrescentar: código.
}.


Árvore-xml em: ['elementos'] fazer: {
	próprio resultado acrescentar: ['>\n'].
	próprio elementmodus := Verdadeiro.
}.

Árvore-xml em: ['textual'] fazer: {
	próprio modo-de-texto := Verdadeiro.
}.


Árvore-xml em: ['fechar'] fazer: {
	>> fechar := próprio elementos pop.
	próprio modo-de-elemento verdadeiro: {
		próprio resultado acrescentar: (['\n</elemento>'] elemento: fechar).
	}, falso: {
		próprio resultado acrescentar: (['></elemento>'] elemento: fechar).
		próprio modo-de-elemento := Verdadeiro.
	}.
}.


Árvore-xml em: ['string'] fazer: { <- próprio resultado. }.


Árvore-xml em: ['sem-xml:'] fazer: { :conteúdo <- conteúdo string
	substituir: ['&'] com: ['&amp;'],
	substituir: ['<'] com: ['&lt;'],
	substituir: ['>'] com: ['&gt;'],
	substituir: ['"'] com: ['&quot;'],
	substituir: ['''] com: ['&apos;'].
}.

