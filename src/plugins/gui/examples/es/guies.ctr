# Título.....: GUIES
# Autor......: Gabor de Mooij (c) copyright 2025
# Fecha......: 2025-03-14
# Licencia...: BSD-3/MIT
# Versión....: 1.1
#
# Descripción:
# Este es un programa de demostración que muestra cómo usar el
# complemento GUI para crear una interfaz gráfica de usuario.
#
# Dependencias:
# - supplementoes
# - abrolxml
# - lvgles

>> gui := Gui nuevo.

gui
	utilizar:
		Ubicación-del-archivo supplementoes,
	utilizar:
		Ubicación-del-archivo arbolxml,
	utilizar:
		Ubicación-del-archivo lvgles.

>> papel-pintado := 
	Imagen nuevo: ['guidemo.png'].

>> cómico :=
	Fuente nuevo 
		fuente: ['Shortcake.ttf']
		tamaño: 40.

>> blanco   := 
	Color nuevo 
		rojo: 255 
		verde: 255
		azul: 255.

>> ejemplo := 
	Árbol-XML sin-xml: 
	['<lv_label text="arbol XML"></lv_label>'].


gui en: ['click:'] hacer: { :id
	(id = 2) verdadero: {
		>> fragmento := LVGLES nuevo
		componente
			elementos
			vista
			extiende: ['objeto'],
			alto: ['100%'],
			ancho: ['100%'],
			opacidad-fondo: 0,
			elementos
				objeto
				alto: ['100%'],
				ancho: ['100%'],
				elementos
					xml: (gui valor-de-relleno: 1),
				cerrar
			cerrar
		cerrar.
		gui xml: fragmento texto nombre: ['x'] en: ['3'].
	}.
}.

>> árbol := LVGLES nuevo.

árbol
	componente
	elementos
		vista
		extiende: ['objeto'],
		eventos: Verdadero,
		alto: ['100%'],
		ancho: ['100%'],
		fondo: papel-pintado,
		elementos
			objeto
			opacidad-fondo: 0,
			eventos: Verdadero,
			borde: 0,
			ancho: ['100%'],
			alto: ['100%'],
			diseño: ['flexible'],
			flujo-flexible: ['columna'],
			elementos
				etiqueta
				fuente: cómico,
				color: blanco,
				texto: ['El arbol XML'],
				cerrar
				area-de-texto
				ancho: ['100%'],
				id: ['1'],
				eventos: Verdadero,
				seleccionable: Verdadero,
				texto: ejemplo,
				cerrar
				botón
				eventos: Verdadero,
				id: ['2'],
				elementos
					etiqueta
					texto: ['dibujar'],
					cerrar
				cerrar
				objeto
				id: ['3'],
				opacidad-fondo: 0,
				borde: 0,
				ancho: ['100%'],
				alto: ['50%'],
				cerrar
			cerrar
		cerrar
	cerrar.

gui 
ancho: 400 alto: 800,
pantalla: árbol texto.

